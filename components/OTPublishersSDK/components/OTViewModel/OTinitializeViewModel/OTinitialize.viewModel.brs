function init()
    m.registry = RegistryUtil()
    m.constant = applicationConstants()
    m.errortype = getErrorType()
    m.errorTags = getErrorTags()
    m.logger = logUtil()
    setfullScreenResolution()
    m.headerParams = {}
    m.subjectIdentifier = ""
    m.isAppIdentifier = false
    m.shouldCreateProfile = false
    m.syncProfile = invalid
    m.authProfileId = ""
    m.isAuthenticatedConsent = false
    _OT_config = getOTconfig()
    if optionalChaining(_OT_config, "version") <> invalid
        m.logger.set(m.errortype.Info, m.errorTags.OneTrust, m.constant.info["704"], _OT_config.version)
    end if

    ' cmpapi init
    m.top.eventlistener = CreateObject("roSGNode", "OTEventliseners")

    m.node = getNode()
    m.global.Addfield("OT_Data", "assocarray", false)
    m.global.OT_Data = {}
    m.style = style()
    OT_Data = {
       fonts: m.style.fonts
       fontSize: m.style.fontSize
    }
    updateOTConfigs(OT_Data)
    
    m.OT_Data = {
     "OT_modelData": {},
    }
    m.consentData = CreateObject("roSGNode", "ContentNode")
    m.consentData.Addfield("purposesStatus", "assocarray", false)
    m.consentData.Addfield("iabVendorsStatus", "assocarray", false)
    m.consentData.Addfield("googleVendorsStatus", "assocarray", false)
    m.consentData.Addfield("sdkStatus", "assocarray", false)
    m.consentData.Addfield("OT_GroupConsents", "assocarray", false)
    m.consentData.Addfield("OT_GroupLIConsents", "assocarray", false)
    m.consentData.Addfield("OT_VendorConsents", "string", false)
    m.consentData.Addfield("OT_vendorLIConsents", "string", false)
    m.consentData.Addfield("OT_AddtlConsent", "string", false)
    m.consentData.Addfield("OT_SdkConsents", "assocarray", false)
    m.consentData["purposesStatus"] = {}
    m.consentData["iabVendorsStatus"] = {}
    m.consentData["googleVendorsStatus"] = {}
    m.consentData["sdkStatus"] = {}
    m.consentData["OT_GroupConsents"] = {}
    m.consentData["OT_GroupLIConsents"] = {}
    m.consentData["OT_SdkConsents"] = {}
    m.consentData["OT_VendorConsents"] = ""
    m.consentData["OT_vendorLIConsents"] = ""
    m.consentData["OT_AddtlConsent"] = ""
    setShouldShowBannerStatus(-1)
    setIsBannerShownStatus(-1)
end function

function isRequiredParams(params as object) as boolean
    if params["language"] = invalid or params["language"] = ""
        m.logger.set(m.errortype.Warning, m.errorTags.OTPublishersHeadlessSDK, "Language", m.constant.warning["900"])
        return false
    end if
    if params["applicationId"] = invalid or params["applicationId"] = ""
        m.logger.set(m.errortype.Warning, m.errorTags.OTPublishersHeadlessSDK, "Application ID", m.constant.warning["900"])
        return false
    end if
    if params["location"] = invalid or params["location"] = ""
        m.logger.set(m.errortype.Warning, m.errorTags.OTPublishersHeadlessSDK, "Location", m.constant.warning["900"])
        return false
    end if
    if params["version"] = invalid or params["version"] = ""
        m.logger.set(m.errortype.Warning, m.errorTags.OTPublishersHeadlessSDK, "SDK version", m.constant.warning["900"])
        return false
    end if
    if params["shouldCreateProfile"] <> invalid and type(params["shouldCreateProfile"]) = "roBoolean"
        m.shouldCreateProfile = params["shouldCreateProfile"]
    end if
    if params.syncProfile <> invalid
        if type(params.syncProfile) <> "roBoolean"
            m.logger.set(m.errortype.Warning, m.errorTags.OTPublishersHeadlessSDK, "Syncprofile", m.constant.warning["901"])
            return false
        else
            setSyncProfile(params["syncProfile"])
        end if
    end if
    if params["syncProfileAuth"] <> invalid
        if type(params["syncProfileAuth"]) = "roString" or type(params["syncProfileAuth"]) = "String"
            setAuthProfileId(params["syncProfileAuth"])
        else
            m.logger.set(m.errortype.Warning, m.errorTags.OTPublishersHeadlessSDK, "syncProfileAuth", m.constant.warning["902"])
            return false
        end if
    end if
    return true
 end function

function OTSdkParams() as object
    params = {
        applicationId: sdkParams().getAppId(),
        location: sdkParams().getLocation(),
        version: sdkParams().getsdkVersion(),
        language: sdkParams().getLanguage(),
        shouldCreateProfile: sdkParams().getShouldCreateProfile(),
        countryCode: sdkParams().getCountryCode(),
        regionCode: sdkParams().getRegionCode(),
        identifier: sdkParams().getIdentifier()
    }
    profileSyncParams = getProfileSyncParams()
    params.Append(profileSyncParams)
    return params
end function

function sdkParams()
    instance = {
        getAppId: function() as dynamic
            headerParams = getHeaderParams()
            return headerParams.applicationId
        end function,
        getLocation: function() as dynamic
            headerParams = getHeaderParams()
            return headerParams.location
        end function,
        getLanguage: function() as dynamic
            headerParams = getHeaderParams()
            return headerParams.language
        end function,
        getsdkVersion: function() as dynamic
            headerParams = getHeaderParams()
            return headerParams.version
        end function
        getShouldCreateProfile: function() as dynamic
            return getCreateProfile()
        end function,
        getSyncProfile: function() as dynamic
            return getSyncProfile()
        end function
        getIdentifier: function() as string
            return getIdentifier()
        end function
        getAuthProfileId: function() as string
            return getAuthProfileId()
        end function
        getCountryCode: function() as dynamic
            headerParams = getHeaderParams()
            return headerParams.countryCode
        end function
        getRegionCode: function() as dynamic
            headerParams = getHeaderParams()
            return headerParams.regionCode
        end function
    }
    return instance
end function

function getProfileSyncParams() as object
    profileParams = {
        syncProfile: sdkParams().getSyncProfile(),
        identifier: sdkParams().getIdentifier(),
        syncProfileAuth: sdkParams().getAuthProfileId()
    }
    return profileParams
end function

function getHeaderParams() as object
    return m.headerParams
end function

function getCreateProfile() as boolean
    return m.shouldCreateProfile
end function

function getSyncProfile() as dynamic
    return m.syncProfile
end function

function getIdentifier() as string
    identifier = m.registry.read("subjectIdentifier")
    if not isString(identifier) then identifier = ""
    return identifier
end function

function getAuthProfileId() as string
    return m.authProfileId
end function

function setSyncProfile(isProfileSync as boolean)
    m.syncProfile = isProfileSync
end function

function setAuthProfileId(authProfileId as string)
    m.authProfileId = authProfileId
end function

function setIdentifier(identifier as string)
    m.subjectIdentifier = identifier
end function

function setfullScreenResolution()
    deviceInfo = CreateObject("roDeviceInfo")
    scale = {
        "FHD": 1,
        "HD": 2/3,
        "SD": 3/8
    }
    screenSize = deviceInfo.GetUIResolution()
    scene = m.top.GetScene()
    m.global.Addfield("screenSize", "assocarray", false)
    if optionalChaining(scene, "currentDesignResolution.height") <> invalid then screenSize.height = scene.currentDesignResolution.height
    if optionalChaining(scene, "currentDesignResolution.width") <> invalid then screenSize.width = scene.currentDesignResolution.width
    if optionalChaining(scene, "currentDesignResolution.resolution") <> invalid then screenSize.name = scene.currentDesignResolution.resolution
    resolution = {
        w: screenSize.width
        h: screenSize.height
        name: screenSize.name
        scale: scale[screenSize.name]
    }
    m.global.screenSize = resolution
end function

function saveConsent(interactionType as string)
    saveLogConsent({interactionType: interactionType}, m)
end function

function updatePurposeConsent(id as string, status as boolean)
    updatelocalConsents(id, status)
end function

function updatePurposeLegitInterest(id as string, status as boolean)
    updatelocalConsents(id, status, "legitInterestCheckBox")
end function

function updateVendorConsent(id as string, status as boolean)
    updatelocalConsents(id, status, "", "iab")
end function

function updateVendorLegitInterest(id as string, status as boolean)
    updatelocalConsents(id, status, "legitInterestCheckBox", "iab")
end function

function updatelocalConsents(id, status, fId ="", vendortype = invalid)
    statustemp = 1
    if isValid(status) and status then statustemp = 0
    focusNode = {
        id: fId,
        itemContent: {
            uId: id,
            parentId: ""
        },
        status: statustemp
    }
    updateConsents(focusNode, m, vendortype) 
end function
